apply plugin: 'com.android.application'

def ENABLE_JAVA_ACTIVITY = true
def ENABLE_NATIVE_ACTIVITY = !ENABLE_JAVA_ACTIVITY

Properties properties = new Properties()
properties.load(new File(rootDir.absolutePath + "/local.properties").newDataInputStream())
// vtk.dir should point to a dir that has vtkConfig.cmake
def vtkDir = properties.getProperty("vtk.dir", null)

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.spherik.myapplication"
        minSdkVersion 25
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [ useJavaActivity:ENABLE_JAVA_ACTIVITY, useNativeActivity:ENABLE_NATIVE_ACTIVITY ]
        externalNativeBuild {
            cmake {
                arguments "-DVTK_DIR=${vtkDir}",
                        "-DENABLE_JAVA_ACTIVITY=${ENABLE_JAVA_ACTIVITY ? 'ON' : 'OFF'}",
                        "-DANDROID_TOOLCHAIN=llvm",
                        "-DCMAKE_SYSTEM_NAME=Android"
                cppFlags "-std=c++11 -frtti -fexceptions -fpermissive -fPIC -w"
            }
        }
        ndk {
            moduleName "NativeVTK"
            abiFilters 'armeabi-v7a' //abiFilters "armeabi-v7a"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
